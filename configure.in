dnl# Process this file with autoconf to produce a configure script.
dnl#

dnl# Initialize autoconf and specify the name of a file in srcdir for
dnl# sanity checking.
dnl#
dnl# The autoconf manual explains that the use of a single argument with
dnl# AC_INIT is obsolete, but the recommended alternative requires
dnl# specifying a package version number, and I think it is inappropriate
dnl# for that information to appear here.  So I am choosing to retain the
dnl# "obsolete" usage.
dnl#
AC_INIT(initscan.c)

# Check that we are not using --srcdir, nor invoking 'configure' from
# someplace other than the directory where it lives.
if test "x$srcdir" != "x."; then
  echo "The specified srcdir, \"$srcdir\", is not \".\"."
  echo "This build system requires that it be built from within "
  echo "its source tree, so srcdir must be \".\"."
  exit 2
fi

dnl# Set @CC@ to "gcc", usually.
AC_PROG_CC

dnl# Set @INSTALL@ to "/usr/bin/install -c", at least on cygwin.
AC_PROG_INSTALL

dnl# When config.status is invoked (which happens implicitly at the end
dnl# of 'configure'), create Makefile by substituting variables.
AC_OUTPUT(config.mk)

dnl# Clear the default help message.  This magic incantation comes from:
dnl# https://stackoverflow.com/questions/29928799/can-you-override-the-default-configure-help-message-generated-as-part-of-running
m4_cleardivert([HELP_BEGIN])dnl
m4_cleardivert([HELP_CANON])dnl
m4_cleardivert([HELP_ENABLE])dnl
m4_cleardivert([HELP_WITH])dnl
m4_cleardivert([HELP_VAR])dnl
m4_cleardivert([HELP_VAR_END])dnl
m4_cleardivert([HELP_END])dnl

dnl# Specify custom --help text, since the default is mostly a
dnl# useless wall of text that has nothing to do with my software.
m4_divert_push([HELP_BEGIN])dnl
if test "$ac_init_help" = "long"; then
  cat <<_ACEOF
This is the configure script for flex.

Usage:

  ./configure [[options]] [[VAR=VALUE]]

Options:

  --prefix=DIR         Install into DIR [[$ac_default_prefix]].

Variables:

  CC                   C compiler.
  CFLAGS               Debug and optimization options for C compiler.
  CPPFLAGS             Preprocessing options.
  LDFLAGS              Link options, such as -LDIR.
  LIBS                 Additional libraries, such as -lsomelib.

_ACEOF

  # Stop after printing the help.
  exit 0
fi
m4_divert_pop([HELP_BEGIN])dnl
# EOF
