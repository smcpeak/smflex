/* yym-read-character.lexpp */
/* Exercise 'yym_read_character' and 'yym_unread_character'. */

%smflex 100
%option c++
%option prefix="Example"
%option yy_read_character
%option yy_unread_character

%{
int mylineno = 0;
%}

string  \"[^\n"]+\"

ws      [ \t]+

alpha   [A-Za-z]
dig     [0-9]
name    ({alpha}|{dig}|\$)({alpha}|{dig}|[_.\-/$])*
num1    [-+]?{dig}+\.?([eE][-+]?{dig}+)?
num2    [-+]?{dig}*\.{dig}+([eE][-+]?{dig}+)?
number  {num1}|{num2}

%%

{ws}    /* skip blanks and tabs */

"/*"    {
        int c;

        while((c = yym_read_character()) != 0)
            {
            if(c == '\n')
                ++mylineno;

            else if(c == '*')
                {
                if((c = yym_read_character()) == '/')
                    break;
                else
                    yym_unread_character(c);
                }
            }
        }

{number}  std::cout << "number " << yym_text() << '\n';

\n        mylineno++;

{name}    std::cout << "name " << yym_text() << '\n';

{string}  std::cout << "string " << yym_text() << '\n';

%%

int main()
{
    // Instantiate the scanner, which by default reads from cin (stdin).
    ExampleFlexLexer lexer;

    // Read tokens until EOF, doing only what the scanner actions do.
    while (lexer.yym_lex() != 0)
        {}

    return 0;
}
