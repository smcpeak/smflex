    // An example of using the smflex C++ scanner class.

%option c++
%option prefix="Example"

%{
int mylineno = 0;
%}

string  \"[^\n"]+\"

ws      [ \t]+

alpha   [A-Za-z]
dig     [0-9]
name    ({alpha}|{dig}|\$)({alpha}|{dig}|[_.\-/$])*
num1    [-+]?{dig}+\.?([eE][-+]?{dig}+)?
num2    [-+]?{dig}*\.{dig}+([eE][-+]?{dig}+)?
number  {num1}|{num2}

%%

{ws}    /* skip blanks and tabs */

"/*"    {
        int c;

        while((c = YY_READ_CHARACTER()) != 0)
            {
            if(c == '\n')
                ++mylineno;

            else if(c == '*')
                {
                if((c = YY_READ_CHARACTER()) == '/')
                    break;
                else
                    YY_UNREAD_CHARACTER(c);
                }
            }
        }

{number}  std::cout << "number " << yym_text() << '\n';

\n        mylineno++;

{name}    std::cout << "name " << yym_text() << '\n';

{string}  std::cout << "string " << yym_text() << '\n';

%%

int main()
{
    // Instantiate the scanner, which by default reads from cin (stdin).
    ExampleFlexLexer lexer;

    // Read tokens until EOF, doing only what the scanner actions do.
    while (lexer.yym_lex() != 0)
        {}

    return 0;
}
